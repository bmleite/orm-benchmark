buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://repo.javalite.io' }
    }
    dependencies {
        classpath group: 'org.javalite', name: 'activejdbc-gradle-plugin', version: '1.4.12'
        classpath group: 'com.h2database', name: 'h2', version: '1.4.192'
    }
}

plugins {
    id "me.champeau.gradle.jmh" version "0.3.1"
    id "nu.studer.jooq" version "1.0.6"
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'org.javalite.activejdbc'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

jooq {
    sample(sourceSets.main) {
        jdbc {
            driver = 'org.h2.Driver'
            url = "jdbc:h2:mem:test;INIT=RUNSCRIPT FROM '${projectDir}/sql/h2/schema.sql'"
            user = 'user'
            password = 'pass'
            schema = 'public'
        }
        generator {
            name = 'org.jooq.util.DefaultGenerator'
            database {
                name = 'org.jooq.util.h2.H2Database'
                inputSchema = 'PUBLIC'
                includes = '.*'
                excludes = ''
            }
            target {
                packageName = 'org.bmleite.model.jooq'
                directory = "src/jooq/java"
            }
        }
    }
}

jmh {
    duplicateClassesStrategy = 'warn'
    benchmarkMode = ['avgt']
    timeUnit = 'ms'
    fork = 1
    iterations = 10
    batchSize = 1
    warmup = '1s'
    warmupForks = 0
    warmupIterations = 10
    warmupBatchSize = 1
}

dependencies {
    // queryDSL
    compile group: 'com.querydsl', name: 'querydsl-sql', version: '4.1.3'

    // Spring JDBC
    compile group: 'org.springframework', name: 'spring-jdbc', version: '4.3.2.RELEASE'

    // ActiveJDBC
    compile group: 'org.javalite', name: 'activejdbc', version: '1.4.12'

    // Hibernate
    compile group: 'org.hibernate', name: 'hibernate-core', version: '5.2.2.Final'

    // Jooq
    compile group: 'org.jooq', name: 'jooq', version: '3.8.4'
    compile group: 'org.jooq', name: 'jooq-meta', version: '3.8.4'
    compile group: 'org.jooq', name: 'jooq-codegen', version: '3.8.4'

    // HikariCP
    compile group: 'com.zaxxer', name: 'HikariCP', version: '2.4.7'

    // https://mvnrepository.com/artifact/org.hibernate.javax.persistence/hibernate-jpa-2.0-api
    compile group: 'org.hibernate.javax.persistence', name: 'hibernate-jpa-2.0-api', version: '1.0.1.Final'

    // https://mvnrepository.com/artifact/org.slf4j/slf4j-simple
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'

    // https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.7'

    // https://mvnrepository.com/artifact/com.h2database/h2
    compile group: 'com.h2database', name: 'h2', version: '1.4.192'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}

apply from: "gradle/querydsl.gradle"